buildscript {
    ext.kotlin_version = '1.0.4'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

ext {
    versionFile = project.file("VERSION")
    versionName = versionFile.readLines()[0].trim()
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

intellij {
    version '15.0.4'
    pluginName 'Emoji Support Plugin'
    updateSinceUntilBuild = false
    publish {
        username 'shiraji'
        password System.getenv('PASSWORD')
        pluginId '' // please fill in
    }
}

group 'com.github.shiraji.emoji'
version versionName

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}


task prepareForNextDevelopment << {
    def versionParts = versionName.split(/\./)
    versionParts[2] = (versionParts[2] as int) + 1
    def newVersion = "${versionParts[0]}.${versionParts[1]}.${versionParts[2]}-SNAPSHOT"
    versionFile.write(newVersion + "\n")
}

task prepareForRelease << {
    def versionParts = versionName.split(/\./)
    versionParts[2] = versionParts[2].split(/-/)[0]
    def newVersion = "${versionParts[0]}.${versionParts[1]}.${versionParts[2]}"
    versionFile.write(newVersion + "\n")
    new File("$projectDir/.travis/release").createNewFile()
}